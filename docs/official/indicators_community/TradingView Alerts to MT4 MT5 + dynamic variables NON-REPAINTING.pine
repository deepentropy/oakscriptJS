// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Peter_O

//@version=5
strategy(title='TradingView Alerts to MT4 MT5 Strategy example', commission_type=strategy.commission.cash_per_order, commission_value=0.00003, overlay=false, default_qty_value=100000, initial_capital=1000)
//
//
// This script was created for educational purposes only.
// It is showing how to use dynamic variables in TradingView alerts.
// And how to execute them in Forex, indices and commodities markets



// **** Entries logic **** {
periodK = input.int(13, title='K', minval=1, inline="stoch", group="Stochastic")
periodD = input.int(3, title='D', minval=1, inline="stoch", group="Stochastic")
smoothK = input.int(4, title='Smooth', minval=1, inline="stoch", group="Stochastic")
k = ta.sma(ta.stoch(close, high, low, periodK), smoothK)
d = ta.sma(k, periodD)
plot(k, title='%K', color=color.new(color.blue, 0))
plot(d, title='%D', color=color.new(color.orange, 0))
h0 = hline(80)
h1 = hline(20)
fill(h0, h1, color=color.new(color.purple, 75))

GoLong = ta.crossover(k, d) and k < 80
GoShort = ta.crossunder(k, d) and k > 20
// } End of entries logic

// **** Pivot-points and stop-loss logic **** {
piv_high = ta.pivothigh(high, 1, 1)
piv_low = ta.pivotlow(low, 1, 1)
var float stoploss_long = low
var float stoploss_short = high

pl = ta.valuewhen(piv_low, piv_low, 0)
ph = ta.valuewhen(piv_high, piv_high, 0)

if GoLong
    stoploss_long := low < pl ? low : pl
    stoploss_long
if GoShort
    stoploss_short := high > ph ? high : ph
    stoploss_short
// } End of Pivot-points and stop-loss logic

TP_type = input.string("pips", options=["percent", "pips"], title="TakeProfit Type")
TakePartialProfitDistance = input.float(150, title="Partial TakeProfit Value")
TakeProfitDistance = input.float(400, title="TakeProfit Value")

TakePartialProfitDistance_Long = TP_type=="percent" ? (TakePartialProfitDistance/100)/syminfo.mintick : TakePartialProfitDistance
TakeProfitDistance_Long = TP_type=="percent" ? (TakeProfitDistance/100)/syminfo.mintick : TakeProfitDistance

TakePartialProfitDistance_Short = TP_type=="percent" ? (TakePartialProfitDistance/100)/syminfo.mintick : TakePartialProfitDistance
TakeProfitDistance_Short = TP_type=="percent" ? (TakeProfitDistance/100)/syminfo.mintick : TakeProfitDistance


strategy.entry('Long', strategy.long, when=GoLong)
strategy.exit('XPartLong', from_entry='Long', qty_percent=50, profit=TakePartialProfitDistance_Long)
strategy.exit('XLong', from_entry='Long', stop=stoploss_long, profit=TakeProfitDistance_Long)
strategy.entry('Short', strategy.short, when=GoShort)
strategy.exit('XPartShort', from_entry='Short', qty_percent=50, profit=TakePartialProfitDistance_Long)
strategy.exit('XShort', from_entry='Short', stop=stoploss_short, profit=TakeProfitDistance_Long)

string_tp1_type = TP_type=="percent" ? ' tp1perc=' : ' tp1='
string_tp_type = TP_type=="percent" ? ' tpperc=' : ' tp='

alertsyntax_golong = 'long slprice=' + str.tostring(stoploss_long) + string_tp1_type + str.tostring(TakePartialProfitDistance) + ' part1=0.5' + string_tp_type + str.tostring(TakeProfitDistance)
alertsyntax_goshort = 'short slprice=' + str.tostring(stoploss_short) + string_tp1_type + str.tostring(TakePartialProfitDistance) + ' part1=0.5' + string_tp_type + str.tostring(TakeProfitDistance)

if GoLong
    alert(message=alertsyntax_golong, freq=alert.freq_once_per_bar_close)
if GoShort
    alert(message=alertsyntax_goshort, freq=alert.freq_once_per_bar_close)


var table alertsDisplayTable = table.new(position.top_right, 1, 5, color.black)
if barstate.islastconfirmedhistory
    table.cell(alertsDisplayTable, 0, 0, "Alert sent at Long Entry:  " + alertsyntax_golong, text_color=color.white)
    table.cell(alertsDisplayTable, 0, 1, "Alert sent at Short Entry:  " + alertsyntax_goshort, text_color=color.white)
    table.cell(alertsDisplayTable, 0, 2, " ")
    table.cell(alertsDisplayTable, 0, 3, "In this use-case exit alerts are not needed, because exit brackets are set already with the entry alert.", text_color=color.white)
    table.cell(alertsDisplayTable, 0, 4, "Also, with TradingConnector's default settings, LONG entry alert will automatically close SHORT position and vice-versa.", text_color=color.white)

