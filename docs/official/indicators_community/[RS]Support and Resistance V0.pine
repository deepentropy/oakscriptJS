// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RicardoSantos

//@version=5
string  TITLE = 'Support and Resistance'
string STITLE = 'SR'
indicator(TITLE, STITLE, true)

bool use_alt_series = input.bool(false, 'Use alternative source?')
float alt_src = input.source(close, 'Alternative source:')

float h = use_alt_series ? alt_src : high
float l = use_alt_series ? alt_src : low

int window1 = input.int(8, 'lookback window 1:')
int window2 = input.int(21, 'lookback window 2:')
float percent_of_range_for_trade_zone = 0.01 * input.float(25.0, 'Percent of the diff to use for trade zone')

float short_term_top = ta.valuewhen(h >= ta.highest(h, window1), h, 0)
float short_term_bot = ta.valuewhen(l <= ta.lowest(l, window1), l, 0)
float long_term_top = ta.valuewhen(h >= ta.highest(h, window2), h, 0)
float long_term_bot = ta.valuewhen(l <= ta.lowest(l, window2), l, 0)

float short_term_resist = ta.change(short_term_top) != 0 ? na : short_term_top
float short_term_suport = ta.change(short_term_bot) != 0 ? na : short_term_bot

float long_term_resist = ta.change(long_term_top) != 0 ? na : long_term_top
float long_term_suport = ta.change(long_term_bot) != 0 ? na : long_term_bot

float long_term_resist_marker = ta.change(fixnan(long_term_resist)) != 0 ? long_term_resist : na
float long_term_suport_marker = ta.change(fixnan(long_term_suport)) != 0 ? long_term_suport : na

color col_shrt_term = color.navy
color col_long_term = color.rgb(33, 150, 243, 70)
color col_long_mark = color.blue
color col_bg_resist = color.rgb(255, 152, 0, 80)
color col_bg_suport = color.rgb(128, 128, 0, 80)
short_term_resist_plot       = plot(short_term_resist      , 'STR', col_shrt_term, 1, plot.style_linebr)
short_term_suport_plot       = plot(short_term_suport      , 'STS', col_shrt_term, 1, plot.style_linebr)
long_term_resist_plot        = plot(long_term_resist       , 'LTR', col_long_term, 4, plot.style_linebr)
long_term_suport_plot        = plot(long_term_suport       , 'LTS', col_long_term, 4, plot.style_linebr)
long_term_resist_marker_plot = plot(long_term_resist_marker, '.R' , col_long_mark, 4, plot.style_circles)
long_term_suport_marker_plot = plot(long_term_suport_marker, '.S' , col_long_mark, 4, plot.style_circles)

fill(short_term_resist_plot, long_term_resist_plot, col_bg_resist, "Resistance")
fill(short_term_suport_plot, long_term_suport_plot, col_bg_suport, "Support")

//  Calculate entry zone:
float short_term_resist_trade_limit = na
float short_term_suport_trade_limit = na

float diff = fixnan(short_term_resist) - fixnan(short_term_suport)

if not na(short_term_resist)
    if not na(short_term_resist_trade_limit[1])
        short_term_resist_trade_limit := short_term_resist_trade_limit[1]
    else
        short_term_resist_trade_limit := short_term_resist - diff * percent_of_range_for_trade_zone

if not na(short_term_suport)
    if not na(short_term_suport_trade_limit[1])
        short_term_suport_trade_limit := short_term_suport_trade_limit[1]
    else
        short_term_suport_trade_limit := short_term_suport + diff * percent_of_range_for_trade_zone

short_term_resist_trade_limit_plot = plot(short_term_resist_trade_limit, 'L', color.red, 1, plot.style_linebr)
short_term_suport_trade_limit_plot = plot(short_term_suport_trade_limit, 'L', color.lime, 1, plot.style_linebr)
