//@version=5
indicator('Weis Wave Volume', shorttitle='WWV', overlay=false, timeframe='')
methodtype = input.string(defval='ATR', options=['ATR', 'Traditional', 'Part of Price'], title='Renko Assignment Method')
methodvalue = input.float(defval=14.0, minval=0, title='Value')
pricesource = input.string(defval='Close', options=['Close', 'Open / Close', 'High / Low'], title='Price Source')
useClose = pricesource == 'Close'
useOpenClose = pricesource == 'Open / Close' or useClose
useTrueRange = input.string(defval='Auto', options=['Always', 'Auto', 'Never'], title='Use True Range instead of Volume')
isOscillating = input(defval=false, title='Oscillating')
normalize = input(defval=false, title='Normalize')
vol = useTrueRange == 'Always' or useTrueRange == 'Auto' and na(volume) ? ta.tr : volume
op = useClose ? close : open
hi = useOpenClose ? close >= op ? close : op : high
lo = useOpenClose ? close <= op ? close : op : low

if methodtype == 'ATR'
    methodvalue := ta.atr(math.round(methodvalue))
    methodvalue
if methodtype == 'Part of Price'
    methodvalue := close / methodvalue
    methodvalue

currclose = float(na)
prevclose = nz(currclose[1])
prevhigh = prevclose + methodvalue
prevlow = prevclose - methodvalue
currclose := hi > prevhigh ? hi : lo < prevlow ? lo : prevclose

direction = int(na)
direction := currclose > prevclose ? 1 : currclose < prevclose ? -1 : nz(direction[1])
directionHasChanged = ta.change(direction) != 0
directionIsUp = direction > 0
directionIsDown = direction < 0

barcount = 1
barcount := not directionHasChanged and normalize ? barcount[1] + barcount : barcount
vol := not directionHasChanged ? vol[1] + vol : vol
res = barcount > 1 ? vol / barcount : vol

plot(isOscillating and directionIsDown ? -res : res, style=plot.style_columns, color=directionIsUp ? color.new(color.green, 75)  : color.new(color.red, 75), linewidth=3, title='Wave Volume')